; pling! (c) copyright Kroc Camen 2019-2020. unless otherwise noted,
; licenced under a MIT licence; you may reuse and modify this code
; how you please as long as you retain the copyright notice
;
; term_print.wla : screen output
;===============================================================================
; label:                    stack:              term-name:
;-------------------------------------------------------------------------------
; term_print_newline:         --                ".n"
; term_print:               b --                "."
; term_print_int:           i --                ".int"
; term_print_hex:           i --                ".hex"
; term_print_bin:           i --                ".bin"
; term_print_stack            --                ".stack"
;

; .n ( -- )
;===============================================================================
; print a newline (CR/LF) character:
;
;-------------------------------------------------------------------------------
        .term   ".n"

term_print_newline:
        ;-----------------------------------------------------------------------
        lda # PET_RETURN
        jmp sys_chrout          ; print, skipping character conversion


; . ( b -- )
;===============================================================================
; print the byte atop the stack as an ASCII character:
; data-type error occurs if top of stack is not a byte
;
; in:   byte    ASCII code to print to screen
;
;-------------------------------------------------------------------------------
        .term   "."

term_print:
        ;-----------------------------------------------------------------------
        ; pop the byte (checking for underflow)
        jsr pop_byte
        ; jump to the system-specific print routine that will do ASCII
        ; conversion to the system's character encoding and handle
        ; writing to the screen
        jmp sys_print


; .int ( i -- )
;===============================================================================
; print the integer atop the stack as a decimal string:
;
; in:   int     integer (byte or word) to print as decimal
;
;-------------------------------------------------------------------------------
        .term   ".int"

term_print_int:
        ;-----------------------------------------------------------------------
        ; pop the integer (byte or word), checking for underflow
        ; TODO: optimised routine for printing a byte
        jsr pop_int

@word:  sta zp_word+LO
        sty zp_word+HI

; adapted from:
; <http://forum.6502.org/viewtopic.php?p=55800#p55800>
;
@print: lda # 0                 ; null delimiter for print
        pha
@div10: ; divide value by 10
        lda # 0
        sta zp_temp2            ; clr BCD
        lda # 16
        sta zp_temp1            ; {>} = loop counter

@loop:  asl zp_word+LO          ; value is gradually replaced
        rol zp_word+HI          ; with the quotient
        rol zp_temp2            ; BCD result is gradually replaced
        lda zp_temp2            ; with the remainder
        sec
        sbc # 10                ; partial BCD >= 10 ?
        bcc +
        sta zp_temp2            ; yes: update the partial result
        inc zp_word+LO          ; set low bit in partial quotient

+       dec zp_temp1
        bne @loop               ; loop 16 times
        
        lda zp_temp2
        ora # '0'               ; convert BCD result to ASCII
        pha                     ; stack digits in ascending
        lda zp_word+LO          ; order ('0' for zero)
        ora zp_word+HI
        bne @div10              ; } until value is 0
        pla

-       jsr sys_chrout          ; print digits in descending
        pla                     ; order until delimiter is
        bne -                   ; encountered

        rts


; .hex ( i -- )
;===============================================================================
; print the integer atop the stack as a hexadecimal string:
;
; in:   int     integer (byte or word) to print as hexadecimal
;
; <http://beebwiki.mdfs.net/Number_output_in_6502_machine_code>
;-------------------------------------------------------------------------------
        .term   ".hex"

term_print_hex:
        ;-----------------------------------------------------------------------
        jsr pop_int
        bcc @byte

        ;-----------------------------------------------------------------------
@word:  sty zp_temp2
        jsr @byte
        lda zp_temp2

        ;-----------------------------------------------------------------------
@byte:  tay

        ; move top nybble to bottom nybble
        lsr
        lsr
        lsr
        lsr
        jsr @conv

        tya
        and # %00001111         ; mask out original bottom nybble

@conv:  ;-----------------------------------------------------------------------
        ; NMOS 6502's (including the 6510) have a bug whereby the decimal flag
        ; is not guaranteed to clear on system-startup or interrupt. this means
        ; that `cld` *must* be used by the system during interrupts to avoid
        ; decimal mode in your code leaking into the interrupt routine!
        ;
        ; (this bug is fixed on CMOS-based 6502's such as the 65C02 & 65C816)
        ;
        ; whilst the C64 KERNAL does clear the decimal mode on startup,
        ; it does not clear it upon ROM-enabled interrupt, meaning that
        ; we must disable interrupts any time we are using decimal mode!
        ;
        ; the time spent in decimal mode / interrupts-off must be kept to
        ; a minimum as it may otherwise impact the C64's interrupt-driven
        ; jiffy-clock -- this is why we don't do the character printing
        ; with interrupts disabled: we don't know how long this will take
        ; on any particular system, and some systems may pause for
        ; user-input when scrolling
        ;
.IFDEF  CPU_6502
        sei                     ; disable interrupts
        sed                     ; turn decimal mode on
.ENDIF
        clc
        adc # $90               ; produce $90-$99 or $00-$05
        adc # $40               ; produce $30-$39 or $41-$46

.IFDEF  CPU_6502
        cld                     ; turn decimal mode off
        cli                     ; re-enable interrupts
.ENDIF
        jmp sys_chrout


; .bin ( i -- )
;===============================================================================
; print the integer atop the stack as a binary string:
;
; in:   int     integer (byte or word) to print as binary
;
;-------------------------------------------------------------------------------
        .term   ".bin"

term_print_bin:
        ;-----------------------------------------------------------------------
        jsr pop_int
        sta zp_temp1
        bcc @byte

@word:  pha
        sty zp_temp1
        jsr @byte
        pla
        sta zp_temp1

@byte:  ldy # '0' >> 1
        lda # 0
        pha
        
        asl zp_temp1
        tya
        rol
        pha
        
        asl zp_temp1
        tya
        rol
        pha

        asl zp_temp1
        tya
        rol
        pha

        asl zp_temp1
        tya
        rol
        pha

        asl zp_temp1
        tya
        rol
        pha

        asl zp_temp1
        tya
        rol
        pha

        asl zp_temp1
        tya
        rol
        pha

        asl zp_temp1
        tya
        rol

-       jsr sys_print
        pla
        bne -
        rts


; .stack ( -- )
;===============================================================================
; pretty-prints the contents of the entire stack, for debugging purposes:
; stack contents are not modified. produces output of this style:
;
;       item type   hex            binary dec   
;       ---------------------------------------- 
;        1st byte $..00 %........00000000     0
;        2nd word $FFFF %1111111111111111 65535
;        3rd tokn END OF DATA
;
; TODO: this will be easier to produce when we have number-formatting routines
; TODO: this should be part of the console and not the core 
;-------------------------------------------------------------------------------
        .term   ".stack"

term_print_stack:
        ;-----------------------------------------------------------------------
        ; is the stack empty?
        cpx # PL_STACK_SIZE
        bcc +

        ; print the "empty" message:
        ;-----------------------------------------------------------------------
        lda #< @empty
        ldy #> @empty
        jmp sys_printstr

@empty: .BYTE   "empty"
        .BYTE   NUL

        ;-----------------------------------------------------------------------
@done:  .plx                    ; restore original stack index
        rts

        ; print the header:
        ;-----------------------------------------------------------------------
+       lda #< @head
        ldy #> @head
        jsr sys_printstr

        ; the stack index measures hardware bytes, not "items" on the stack,
        ; as items can be bytes or words, so we need a counter for items
        lda # 1
        sta zp_index

        ; backup the current stack index; we'll walk down
        ; the stack, as if we were popping every item
        .phx

        ; walk the stack:
        ;=======================================================================
@item:  lda zp_type, x          ; read the data-type of the current item
        cmp # TOKEN_STK         ; is it the bottom of the stack?
        beq @done               ; yes? finish printing
        
        pha                     ; put aside the data type;
        lda zp_index            ; begin printing the line-item
        
        ; print the item number:
        ;-----------------------------------------------------------------------
        ; this routine for converting a byte to decimal digits is adapted from:
        ; <https://codebase64.org/doku.php?id=base:tiny_.a_to_ascii_routine>
        ;
        ; we happen to know that this cannot be in the hundreds, at least on
        ; the 6502, as Pling's stack is not > 99 bytes; the data-type stack
        ; doubles the space required and there's only 256 bytes in zero-page!
        ;
        ; hundreds:
        ;-----------------------------------------------------------------------
        sec
        ldy # '0'-1             ; increment using an ASCII digit!
-       iny                     ; increment hundreds-digit,
        sbc # 100               ; for each 100 decimal subtracted
        bcs -                   ; keep going?
        ;;pha                     ; (put aside the remainder)

        ;;tya                     ; get hundreds-digit:
        ;;cmp # '0'               ; is it a zero?
        ;;bne +                   ; if not, print the digit as-is
        ;;lda # PET_SPC           ; (print a space instead of a leading "0")
+       ;;jsr sys_chrout          ; print the digit
        ;;clc                     ; (carry state unknown)

        ; tens & ones:
        ;-----------------------------------------------------------------------
        ;;pla                     ; retrieve the working remainder
        ldy # '9'+1             ; decrement using an ASCII digit!
-       dey                     ; decrement the tens-digit,
        adc # 10                ; for each 10 decimal subtracted
        bmi -                   ; keep going?

        adc # '0'-1             ; make it an ASCII numeral
        pha                     ; (put aside the remainder!)

        tya                     ; get tens-digit:
        cmp # '0'               ; is the digit a zero?
        bne +                   ; if not, print the digit as-is
        lda # ' '               ; (print a space instead of a leading "0")
+       jsr sys_print           ; print the digit
        clc                     ; (carry state unknown)

        pla                     ; retrieve the 1s-digit
        tay
        jsr sys_print           ; print the digit

        cpy # '5'
        bne +
        ldy # '4'
+       lda @ord1-'0', y
        jsr sys_print
        lda @ord2-'0', y
        jsr sys_print

        ; data-type:
        ;-----------------------------------------------------------------------
        pla
        bmi @word               ; it's a word...
        beq @byte               ; it's a byte...

        ; when you have eliminated the impossible,
        ; whatever remains, however improbable,
        ; must be a token
@token: lda #< @typet
        ldy #> @typet
        jsr sys_printstr
        
        lda # PET_RETURN
        jsr sys_print
        
        .plx
        rts

        ;-----------------------------------------------------------------------
@word:  lda #< @typew
        ldy #> @typew
        jsr sys_printstr

        lda zp_stack+0, x
        ldy zp_stack+1, x
        jsr term_print_hex@word

        lda #< @binw
        ldy #> @binw
        jsr sys_printstr

        lda zp_stack+0, x
        ldy zp_stack+1, x
        jsr term_print_bin@word

        lda # ' '
        jsr sys_print

        lda zp_stack+0, x
        ldy zp_stack+1, x
        jsr term_print_int@word

        lda # PET_RETURN
        jsr sys_print

        inx
        inx
        inc zp_index
        jmp @item

        ;-----------------------------------------------------------------------
@byte:  lda #< @typeb
        ldy #> @typeb
        jsr sys_printstr

        lda zp_stack, x
        jsr term_print_hex@byte

        lda #< @binb
        ldy #> @binb
        jsr sys_printstr

        lda zp_stack+0, x
        ldy zp_stack+1, x
        jsr term_print_bin@byte

        lda # ' '
        jsr sys_print

        lda zp_stack+0, x
        ldy # 0
        jsr term_print_int@word

        lda # PET_RETURN
        jsr sys_print

        inx
        inc zp_index
        jmp @item

        ;=======================================================================
@head:  .BYTE   PET_YELLOW, PET_RVRS_ON
        .BYTE   "item type   hex            binary dec   "
        .BYTE   PET_WHITE, PET_RVRS_OFF
        .BYTE   NUL
        ;-----------------------------------------------------------------------
        ;        0    1    2    3    4...
@ord1:  .BYTE   "t", "s", "n", "r", "t"
@ord2:  .BYTE   "h", "t", "d", "d", "h"
        ;-----------------------------------------------------------------------
@typet: .BYTE   " tokn ", NUL
@typew: .BYTE   " word $", NUL
@typeb: .BYTE   " byte $..", NUL
        ;-----------------------------------------------------------------------
@binb:  .BYTE   " %........", NUL
@binw:  .BYTE   " %", NUL
