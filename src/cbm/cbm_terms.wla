; pling! (c) copyright Kroc Camen 2019-2020. unless otherwise noted,
; licenced under a MIT licence; you may reuse and modify this code
; how you please as long as you retain the copyright notice
;
; cbm_terms.wla : Commodore-specific terms
;===============================================================================

sys_printstr:
;===============================================================================
; system specific routine for printing an *ASCII*, null-terminated string:
;
; in:   A       string address, lo-byte
;       Y       string address, hi-byte
;
; out:  A, Y    (clobbered)
;       X       (preserved)
;
;-------------------------------------------------------------------------------
        sta zp_addr+LO          ; load the string address into our indexer
        sty zp_addr+HI
        
        ldy # 0
-       lda [zp_addr], y        ; read a character
        beq +                   ; null-terminator? exit
        jsr sys_print           ; convert to ASCII and output
        iny                     ; move to the next character
        bne -                   ; loop (but also end at 255 characters)

+       rts


sys_print:
;===============================================================================
; system-specific routine for printing a single *ASCII* character:
;
; in:   A       ASCII-code
;
; out:  A       (clobbered)
;       X, Y    (preserved)
;
;-------------------------------------------------------------------------------
        jsr asc2pet             ; convert ASCII code to PETSCII

sys_emit:
        ;-----------------------------------------------------------------------
        jmp KERNAL_CHROUT


pet2asc:
;===============================================================================
; convert a PETSCII character code to ASCII:
;
; TODO: use a lookup-table for ROM-version?
;       (would require preserving Y)
;
; in:   A       character code
;
; out:  A       character code, converted  
;       X, Y    (preserved)
;                                                                       ;cycles
;-------------------------------------------------------------------------------
        ; is the character in the PETSCII upper-case range?
        ; note that the "case bit" in PETSCII is %10000000,
        ; not %00100000 as in ASCII
        ;
        cmp # PET_A_UP          ; "A" and above?
        bcc +                   ; no -- skip ahead
        cmp # PET_Z_UP+1        ; "Z" and below?
        bcs +                   ; no -- skip ahead

        ; adjust PETSCII upper-case $C1..$DA down to ASCII upper-case $41..$5A
        ; NOTE: carry is clear, so this subtracts an additional 1 for us!
        sbc # $80-1
        rts
        
        ; is the character in the PETSCII lower-case range?
        ;
+       cmp # PET_A_LO          ; "a" and above?
        bcc +                   ; no -- exit
        cmp # PET_Z_LO+1        ; "z" and below?
        bcs +                   ; no -- exit

        ; adjust PETSCII lower-case $41..$5A
        ; up to ASCII lower-case $61..$7A
        adc # $20

+       rts

asc2pet:
;===============================================================================
; convert an ASCII character code to PETSCII:
;
; TODO: use a lookup-table for ROM-version?
;       (would require preserving Y)
;
; in:   A       character code
;
; out:  A       character code, converted  
;       X, Y    (preserved)
;                                                                       ;cycles
;-------------------------------------------------------------------------------
        ; is the character in the ASCII lower-case range?
        ;
        cmp # $61               ; "a" and above?
        bcc +                   ; no -- skip ahead
        cmp # $7a+1             ; "z" and below?
        bcs +                   ; no -- skip ahead

        ; adjust ASCII lower-case $61..$7A down to PETSCII lower-case $41..$5A
        ; NOTE: carry is clear, so this subtracts an additional 1 for us!
        sbc # $20-1
        rts
        
        ; is the character in the ASCII upper-case range?
        ;
+       cmp # $41               ; "A" and above?
        bcc +                   ; no -- exit
        cmp # $5a+1             ; "z" and below?
        bcs +                   ; no -- exit

        ; adjust ASCII upper-case $41..$5A
        ; up to PETSCII upper-case $C1..$DA
        adc # $80

+       rts